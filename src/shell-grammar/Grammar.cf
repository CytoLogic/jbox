-- jsh grammar

token WordToken ( (char - ('$' | '=' | ';' | '"' | ' ' | '\t' | '\n' | '\r') )+ | ('\\' '"')+ | ('\\' '$')+ | ('\\' '=')+| ('\\' ';')+)+ ;
token StringToken ('"' ( (char - '"') | ('\\' '"') )* '"') ; 
token VariableToken ('$' (char - ('$' | '"' | ' ' | '\t' | '\n' | '\r' | '=') )+ ) ;   

comment "/*" "*/" ;
comment "//" "\n" ;

separator nonempty Job ";" ;
separator nonempty ShellToken " " ;

entrypoints Input ;

In. Input ::= [Job] ;

AssigJob. Job ::= WordToken "=" CommandLine ;
AIChatJob. Job ::= "@" AIQuery ;
AIExecJob. Job ::= "@!" AIQuery ;
FGJob. Job ::= CommandLine ;
BGJob. Job ::= CommandLine "&" ;

CmdLine. CommandLine ::= CommandPart OptionalInputRedirection OptionalOutputRedirection ;

SnglCmd. CommandPart ::= CommandUnit ;
PipeCmd. CommandPart ::= CommandUnit "|" CommandPart ;

CmdUnit. CommandUnit ::= [ShellToken] ;

NoInRedir. OptionalInputRedirection ::= ;
InRedir. OptionalInputRedirection ::= "<" ShellToken ;
NoOutRedir. OptionalOutputRedirection ::= ;
OutRedir. OptionalOutputRedirection ::= ">" ShellToken ;

Var. ShellToken ::= VariableToken ;
Wrd. ShellToken ::= WordToken ;
Str. ShellToken ::= StringToken ;

FAIQ. AIQuery ::= [ShellToken] ;
EAIQ. AIQuery ::= ;

